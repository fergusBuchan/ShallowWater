#pragma kernel Pass0
#pragma kernel Pass1
#pragma kernel Pass2
#pragma kernel SplashPass

//struct for particle (yeah it could just be a float3 i know but who are you? My dad? I get to use u,v and h instad of x/y/z so it's kind of useful)
struct waterParticle {
    float u;//horizontal veolocity
    float v;//vertical veolocity
    float h;//height
};

// array of particles 
RWTexture2D<float4>  waterSimParticles_0;
// texture for the force generated through physics objects
RWTexture2D<float4> splashTexture_0;
// this is the texture we use for the heightmap (it doesn't have intermidiate step numbers in it)
RWTexture2D<float4>  hieghtMap;

// length of the sides of the image
float dimX;
float dimY;

// time since last simulation
float deltaTime;

//gravity
const static float g = 9.8;

/*

to fit all these equations on screen we need some abbrviated varible names
this daigram should show how things are orginised:

[  ][v1][  ][  ]
[  ][v0][  ][  ]
[h_][p ][h0][h1]
[  ][v_][  ][  ]

-> p is the particle we are solving for
-> h_,v_,h0,v0 are calulated as an intermediate step, they won't show up in the hieght map
-> h1 and v1 represent other particles

*/

[numthreads(8, 8, 1)]
void Pass0(uint3 id : SV_DispatchThreadID)
{

    waterSimParticles_0[int2(id.x, id.y)] = waterSimParticles_0[int2(id.x, id.y)] +  float4(0, 0, 10.0, 0.0);

}
// this function inits the sim, if the height of the particles is ever negative it breaks the sim, so it's nice to have a buffer

[numthreads(8,8,1)]
void Pass1(uint3 id : SV_DispatchThreadID)
//threadNo is the thread we are on, index is the place within the thread
{

    float dt = deltaTime;
    float2 dim = float2(100 / dimX, 100 / dimY);
    // this represents the distance between particles

    uint2 currentCoords = int2(id.x * 2, id.y * 2);

    waterParticle p;
    p.u = waterSimParticles_0[currentCoords].x;
    p.v = waterSimParticles_0[currentCoords].y;
    p.h = waterSimParticles_0[currentCoords].z;

    waterParticle h1;
    h1.u = waterSimParticles_0[currentCoords + int2(2, 0)].x;
    h1.v = waterSimParticles_0[currentCoords + int2(2, 0)].y;
    h1.h = waterSimParticles_0[currentCoords + int2(2, 0)].z;

    waterParticle v1;
    v1.u = waterSimParticles_0[currentCoords + int2(0, 2)].x;
    v1.v = waterSimParticles_0[currentCoords + int2(0, 2)].y;
    v1.h = waterSimParticles_0[currentCoords + int2(0, 2)].z;

    // just some orginisation stuff, just sampling textures and putting them into structs


    float h; //height
    float uh;//horizontal momentum
    float vh;//vertical momentum

    if (id.x < dimX - 1) {
    //to stop us editing particles off the edge of the array
        h =
            (0.5) * (h1.h + p.h)
            - (dt / dim.x) * ( (h1.u * h1.h) - (p.u * p.h) )
            ;
        uh =
            (0.5) * ( (h1.u * h1.h) + (p.u * p.h) )
            - (dt / dim.x) * (  ( (pow(h1.u, 2) * h1.h) + (0.5 * g * pow(h1.h, 2)) ) - ( (pow(p.u, 2) * p.h) + (0.5 * g * pow(p.h, 2)) )  )
            ;
        vh =
            (0.5) * ((h1.v * h1.h) + (p.v * p.h))
            - (dt / dim.x) * ( (h1.u * h1.v * h1.h) - (p.u * p.v * p.h) )
            ;

        waterParticle h0;

        h0.u = uh / h;// convert momentum to velocity

        h0.v = vh / h;// ==

        h0.h = h;

        waterSimParticles_0[currentCoords + int2(1, 0)] = float4(h0.u, h0.v, h0.h, 1.0);
       // updating sim with new itermidate values
    }

   
    if (id.y < dimY - 1) {
    //to stop us editing particles off the edge of the image

        h =
            (0.5) * ((v1.h) + (p.h))
            - (0.5) *  (dt / dim.y) * ((v1.v * v1.h) - (p.v * p.h))
            ;
        uh =
            (0.5) * ((v1.u * v1.h) + (p.u * p.h))
            - (0.5) * (dt / dim.y) * ((v1.u * v1.v * v1.h) - (p.u * p.v * p.h))
            ;
        vh =
            (0.5) * ((v1.v * v1.h) + (p.v * p.h))
            - (0.5) * (dt / dim.y) * (  ( (pow(v1.v, 2) * v1.h) + (0.5 * g * pow(v1.h, 2)) ) - ( (pow(p.v, 2) * p.h) + (0.5 * g * pow(p.h, 2)) )  )
            ;

        waterParticle v0;

        v0.u = uh / h;// convert momentum to velocity

        v0.v = vh / h;// ==

        v0.h = h;

        waterSimParticles_0[currentCoords + int2(0, 1)] = float4(v0.u, v0.v, v0.h, 1.0);
       // updating sim with new itermidate values
    }

    // yeah this physics stuff? I'm just as lost as you are
}


[numthreads(8, 8, 1)]
void Pass2(uint3 id : SV_DispatchThreadID)
{
    
    float dt = deltaTime;
    float2 dim = float2(100 / dimX, 100 / dimY);
    // this represents the distance between particles

    int2 currentCoords = int2(id.x * 2 ,id.y * 2);

    waterParticle p;
    p.u = waterSimParticles_0[currentCoords].x;
    p.v = waterSimParticles_0[currentCoords].y;
    p.h = waterSimParticles_0[currentCoords].z;
    

    waterParticle h0; 
    if (id.x < dimX - 1) {// just catching when we're not at the edge of the image, so we don't look for data that's not there
        h0.u = waterSimParticles_0[currentCoords + int2(1, 0)].x;
        h0.v = waterSimParticles_0[currentCoords + int2(1, 0)].y;
        h0.h = waterSimParticles_0[currentCoords + int2(1, 0)].z;
    }
    else {
        h0.u = 0.0; h0.v = 0.0; h0.h = 0.0;
    }

    waterParticle h_;
    if (id.x > 1) {//==
        h_.u = waterSimParticles_0[currentCoords + int2(-1, 0)].x;
        h_.v = waterSimParticles_0[currentCoords + int2(-1, 0)].y;
        h_.h = waterSimParticles_0[currentCoords + int2(-1, 0)].z;

    }
    else {
        h_.u = 0.0; h_.v = 0.0; h_.h = 0.0;

    }

    waterParticle v0;
    if (id.y < dimY - 1) {//==
        v0.u = waterSimParticles_0[currentCoords + int2(0, 1)].x;
        v0.v = waterSimParticles_0[currentCoords + int2(0, 1)].y;
        v0.h = waterSimParticles_0[currentCoords + int2(0, 1)].z;
    }
    else {
        v0.u = 0.0; v0.v = 0.0; v0.h = 0.0;
    }

    waterParticle v_;
    if (id.y > 1) {//==
        v_.u = waterSimParticles_0[currentCoords + int2(0, -1)].x;
        v_.v = waterSimParticles_0[currentCoords + int2(0, -1)].y;
        v_.h = waterSimParticles_0[currentCoords + int2(0, -1)].z;
    }
    else {
        v_.u = 0.0; v_.v = 0.0; v_.h = 0.0;
    }


    float h; //height
    float uh;//horizontal momentum
    float vh;//vertical momentum

    // just some orginisation stuff, just sampling textures and putting them into structs

    h = 
        (p.h) 
        - (  (dt / dim.x) * ( (h0.u * h0.h) - (h_.u * h_.h) )  )
        - (  (dt / dim.y) * ( (v0.v * v0.h) - (v_.v * v_.h) )  )
        ;


    if (id.x > 1 && id.x < dimX - 1) {
        uh =
            (p.u * p.h)
            - (dt / dim.x) * (  ( (pow(h0.u, 2) * h0.h) + (0.5 * g * pow(h0.h, 2)) ) - ( (pow(h_.u, 2) * h_.h) + (0.5 * g * pow(h_.h, 2)) )  )
            - (dt / dim.y) * ( (v0.u * v0.v * v0.h) - (v_.u * v_.v * v_.h) )
            ;
    }
    else {
       uh = 0.0;
        //particles on the horizontal sides have u set to zero
    }

    if (id.y > 1 && id.y < dimY - 1) {
        vh =
            (p.v * p.h)
            - (dt / dim.x) * ( (h0.u * h0.v * h0.h) - (h_.u * h_.v * h_.h) )
            - (dt / dim.y) * (  ( (pow(v0.v, 2) * h0.h) + (0.5 * g * pow(v0.h, 2)) ) - ( (pow(v_.v, 2) * h_.h) + (0.5 * g * pow(v_.h, 2)) )  )
            ;
    }
    else {
        vh = 0.0;
        //particles on the vertical sides have v set to zero
    }

    waterParticle newP;

    newP.u = uh / h;
    newP.u *= 0.999;//<- this is so waves don't last forever

    newP.v = vh / h;
    newP.v *= 0.999;

    newP.h = h;

    waterSimParticles_0[currentCoords] = float4(newP.u, newP.v, newP.h,1.0);
    // updating the particle texture

    hieghtMap[id.xy] = float4(newP.h - 10.0f, newP.h - 10.0f, newP.h - 10.0f, 1.0);
    // converting the data into a height map
}

[numthreads(8, 8, 1)]
void SplashPass(uint3 id : SV_DispatchThreadID) {

    float4 currentV =  waterSimParticles_0[int2(id.x * 2, id.y * 2)];
    float4 splashV =  splashTexture_0[int2(id.x, id.y)];


    float2 dir = float2(0.0f,0.0f);
    if(splashTexture_0[int2(id.x, id.y) + int2(1,0)].y == 0.0f){
        dir.x += 1.0f;
    }
    if(splashTexture_0[int2(id.x, id.y) + int2(-1,0)].y == 0.0f){
        dir.x += -1.0f;
    }
    if(splashTexture_0[int2(id.x, id.y) + int2(0,1)].y == 0.0f){
        dir.y += 1.0f;
    }
    if(splashTexture_0[int2(id.x, id.y) + int2(0,-1)].y == 0.0f){
        dir.y += -1.0f;
    }

    if(length(dir) > 0.0){
        dir = normalize(dir);
    }

    splashV.x += dir.x * abs(splashV.y);
    splashV.z += dir.y * abs(splashV.y);

    // here we're calculating a vector for the force in the y direction,
    // since our sim is 2d we have to to change the 3d veolicty to work in 2d space
    // basicly we make a vector that pionts away from other pixels with y velocity

    if(splashV.x > 0){
        if(splashV.x > currentV.x){
            currentV.x = splashV.x;
        }
    }
    else if (splashV.x < 0){
        if(splashV.x < currentV.x){
            currentV.x = splashV.x;
        }
    }

    if(splashV.z > 0){
        if(splashV.z > currentV.y){
            currentV.y = splashV.z;    //<- nb the z velocity in the game world is the y velocity in the simulation
        }
    }
    else if (splashV.z < 0){
        if(splashV.z < currentV.y){
            currentV.y = splashV.z;
        }
    }


    // if the horizontal/vertical velocity of the object is greater than^ the paritcle's velocity, 
    // then we set the particle's velocity to the object's
    // ^ relative to the direction, if the object velocity is negative it will be less than

    waterSimParticles_0[int2(id.x * 2, id.y * 2)] = currentV;
}
// I needed a way to add spashes to the sim so when you click it it splashes but I also thought it would be cool if you could 
// simulate rain on a puddle so it's kind of just a texture merge 